title: Coalition Affairs
new-page: true
is-card: true
tags: govt_affairs
on-arrival: month_actions += 1; coalition_affairs_timer = 5
view-if: spd_in_government = 1 and coalition_dissent >= 1 and coalition_affairs_timer = 0 and (in_grand_coalition or in_weimar_coalition or in_popular_front)
card-image: img/muller_cabinet.jpg

= Coalition Affairs

Our coalition is in trouble. The right-leaning members of the Center [? if in_grand_coalition: and DVP ?] are constantly criticizing us for our policies, and are threatening to call a vote of no confidence to collapse this government[? if spd_prussia :, or to do the same for our government in the state of Prussia ?].

How do we reduce tensions with the coalition?

- @promise_cuts: Agree to the bourgeois plans to cut welfare.
- @resources: Use our party resources to support our allies.
- @give_up_ministries: We could give up a ministry we hold.
- @bring_down: Instead, we will bring down the government with a no-confidence vote, and call for new elections.
- @no_change: Take none of these actions.
- @easy_discard


@promise_cuts
on-arrival: workers_spd -= 5; unemployed_spd -= 6; workers_kpd += 5; unemployed_kpd += 3; unemployed_nsdap += 3*(nsdap_effectiveness); left_dissent += 10; labor_dissent += 5; welfare -= 1; budget += 1; coalition_dissent = 0; nsdap_effectiveness += 0.05; nsdap_effectiveness += 0.05 if unemployed > 10;

Fine. By cutting welfare, we have reduced dissent within the coalition.

@resources
subtitle: -2 resources
view-if: historical_mode = 0
unavailable-subtitle: Not enough resources (need 2).
choose-if: resources >= 2
on-arrival: resources -= 2; coalition_dissent -= 1

By transferring campaign funds to our coalition partners, we can make our policies more acceptable to them. Some would call this corruption, but perhaps it is necessary to save democracy.

@give_up_ministries
choose-if: ministries_held >= 4
unavailable-subtitle: We do not hold enough ministries for the Right to accept a ministry handover.

We could give up a ministry or multiple ministries to the bourgeois parties in exchange for their support. However, this will ignite concerns among the leftist and labor factions within our party, who see this as a “sign of weakness” towards the bourgeois and a betrayal of the working class.

- @give_up_labor_ministry: Give up the Labor Ministry to the Centre Party
- @give_up_interior_ministry: Give up the Interior Ministry to the Centre Party
- @give_up_finance_ministry: Give up the Finance Ministry to the DVP
- @give_up_economic_ministry: Give up the Economic Ministry to the DVP
- @give_up_justice_ministry: Give up the Justice Ministry to the DDP
- @give_up_foreign_ministry: Give up the Foreign Ministry to the DVP
- @give_up_agriculture_ministry: Give up the Agriculture Ministry to the DDP
- @give_up_reichswehr_ministry: Give up the Reichswehr Ministry to General Groener 
- @another_way: We must deal with this another way to avoid dissent within our party.

@give_up_labor_ministry
choose-if: labor_minister_party == "SPD"
unavailable-subtitle: We do not hold the Labor Ministry
subtitle: This will upset labor and the unions.
on-arrival: coalition_dissent -= 1; labor_minister_party = "Z"; labor_minister = "Stegerwald"; labor_dissent += 15; left_dissent += 15; z_relation += 5

Fine, we have given up the Labor Ministry. However, the bourgeois see this ministry as a “less important” ministry and are still deeply suspicious of our actions. The Labor and Left factions also sees this as a betrayal and an give to the bourgeois.

@give_up_interior_ministry
choose-if: interior_minister_party == "SPD"
unavailable-subtitle: We do not hold the Interior Ministry
subtitle: This all upset all factions of our party
on-arrival : coalition_dissent -= 1; interior_minister_party = "Z"; interior_minister = "Wirth"; reformist-dissent += 5; left_dissent += 15; centrist_dissent += 10; labor_dissent += 5; z_relation += 5

Fine, we have given up the Interior Ministry. This decision is opposed by all factions of our party. Leftists see this as a complete sell-out to the bourgeois. Even reformists are concerned with our decision, and the bourgeois is still suspicious of us, despite giving up the Interior Ministry. 

@give_up_finance_ministry
choose-if: finance_minister_party == "SPD"
unavailable-subtitle: We do not hold the Finance Ministry
subtitle: This will displease the Labor and Left factions.
on-arrival: finance_minister_party = "DVP"; finance_minister = "Moldenhauer"; coalition_dissent -= 2; centrist_dissent += 10; leftist_dissent += 15; labor_dissent += 10; dvp_relation += 5

Fine, we have given up the Finance Ministry. This is criticized by centrists, labor, and leftists as another handover to the bourgeois. The DVP is pleased to see us give up the finance ministry, seeing an end to our “socialist” financial policies. 

@give_up_economic_ministry
choose-if: economic_minister_party == "SPD"
unavailable-subtitle: We do not hold the Economic Ministry
subtitle: This will [? if black_thursday_seen == 1: significantly ?] upset the Labor and Left factions.
on-arrival: economic_minister_party = "DVP"; economic_minister = "Curtius"; coalition_dissent -= 2; labor_dissent += 10; labor_dissent += 5 if year >= 1929 and month >= 10; centrist_dissent += 5; reformist_dissent += 5; leftist_dissent += 15; leftist_dissent += 5 if year >= 1929 and month >= 10; ddp_relation += 5

Fine, we have given up the Economic Ministry. This action is opposed by all factions of our party. [? if black_thursday_seen == 1: The Labor and Left factions are disgraced by this action, preventing them from implementing their economic plans in the midst of this crisis ?]

@give_up_justice_ministry
choose-if: justice_minister_party == "SPD"
unavailable-subtitle: We do not hold the Justice Ministry
subtitle: This is opposed by all factions of our party [? if judicial_reform <= 2: , including Neorevisionists ?]
on-arrival: justice_minister_party = "DDP"; justice_minister = "Koch-Weser"; coalition_dissent -= 2; labor_dissent += 5; centrist_dissent += 5; reformist_dissent += 5; reformist_dissent += 5 if judicial_reform <= 2; leftist_dissent += 15; neorevisionist_dissent += 5 if judicial_reform <= 2

Fine, we have given up the Justice Ministry. Reformists are [? if judicial_reform > 2: bothered ?][? if judicial_reform <= 2: disgusted ?] by the action, seeing it as compromising the stability of the Republic, while leftists strongly oppose the action due to another handover to the bourgeois. [? if judicial_reform <= 2:  Neorevisionists also oppose our action due to its perceived harm to the Republic. ?]

@give_up_foreign_ministry
choose-if: foreign_minister_party == "SPD"
unavailable-subtitle: We do not hold the Foreign Ministry
subtitle: This is opposed by the Centrist faction, especially Breitscheid, and the Left faction
on-arrival: foreign_minister_party = "DVP"; foreign_minister = "Stresemann" if stresemann_dead == 0; foreign_minister = "Curtis" if stresemann_dead == 1; coalition_dissent -= 2; centrist_dissent += 10; leftist_dissent += 15; dvp_relation += 10; z_relation += 5 if z_party_leader == "Kaas"

Fine, we have given up the Foreign Ministry. The DVP [? if z_party_leader == "Kaas": and Center ?] is pleased by this decision, while all factions of our party are united against this decision. Leftists oppose this action more strongly than any faction, wanting no overtures to the bourgeois. 

@give_up_agriculture_ministry
choose-if: agriculture_minister_party == "SPD"
unavailable-subtitle: We do not hold the Agriculture Ministry
subtitle: This action is opposed the the Left and Labor factions
on-arrival: agriculture_minister_party = "DDP"; agriculture_minister = "Dietrich"; labor_dissent += 5; leftist_dissent += 10; leftist_dissent += 5 if land_reform < 1; ddp_relation += 5; coalition_dissent -= 2

Fine, we have given up the Agriculture Ministry. Labor and leftists in our party oppose this decision due to their strong interest in agricultural policy, [? if land_reform < 1: especially land reform. ?]

@give_up_reichswehr_ministry
choose-if: reichswehr_ministry_party == "SPD"
unavailable-subtitle: We do not hold the Reichswehr Ministry
subtitle: This action is opposed by all factions of our party, especially leftists and neorevisionists
on-arrival: reichswehr_minister_party = "I"; reichswehr_minister = "Groener"; leftist_dissent += 15; neorevisionist_dissent += 10; reformist_dissent += 5; labor_dissent += 5; centrist_dissent += 5; dvp_relation += 10; z_relation += 10 if z_party_leader == "Kaas"; z_relation += 5 if z_party_leader == "Joos"; coalition_dissent = 0

Fine, we have given up the Reichswehr Ministry. This decision scares neorevisionists and leftists who do not want to see the military in the hands of reactionary, or worse, fascist forces. Other factions are slightly opposed to this policy. The Centre and DVP are pleased that we gave up the Reichswehr Ministry. 

@another_way

- @promise_cuts: Agree to the bourgeois plans to cut welfare.
- @resources: Use our party resources to support our allies.
- @give_up_ministries: We could give up a ministry we hold.
- @bring_down: Instead, we will bring down the government with a no-confidence vote, and call for new elections.
- @no_change: Take none of these actions.
- @easy_discard

@no_change

No. We will not do this.

@bring_down
subtitle: Bring down this government before our opponents do.
on-arrival: spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; chancellor = "Brüning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 5; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 5; nsdap_effectiveness += 0.1; dnvp_effectiveness += 0.05

We have called for a vote of no confidence in ourselves, and have brought down this government. Chancellor [+ chancellor +] currently rules as acting chancellor.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

