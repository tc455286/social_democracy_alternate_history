title: The Right calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
view-if: spd_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and coalition_dissent >= 3) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and spd_r < 50 and not constructive_vonc
tags: event

# Options: spend relations to reduce coalition_dissent, ask for kpd to abstain
# (lmao KPD toleration arc...)

= Vote of No Confidence

[? if in_grand_coalition : The DVP ?][? if in_weimar_coalition or in_popular_front or in_minority_government: The Center Party ?] has called for a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

- @try_assuage: Try to assuage our partners.
- @support_kpd: Could we get the KPD to abstain?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage our partners?

- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @give_up_ministries: Give up a ministry we hold.
- @resources: Use our resources to reduce dissent within the coalition.
- @support_kpd: Could we get the KPD to abstain?
- @let_it_happen: Never mind. Let the vote happen.

@give_up_prussia
view-if: spd_prussia == 1
on-arrival: coalition_dissent = 0; spd_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_spd -= 5; rural_spd -= 5; 

Fine. We no longer control the government of Prussia. The vote has been called off.

@austerity
view-if: welfare >= 0
on-arrival: coalition_dissent = 0; workers_spd -= 10; umemployed_spd -= 10; workers_kpd += 7; workers_nsdap += 3; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; kpd_coalition_dissent += 1; kpd_relation -= 10 if in_popular_front

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@give_up_ministries
# view-if: ministries held >= 4

We could give up a ministry or multiple ministries to the bourgeois parties in exchange for their support. However, this will ignite concerns among the leftist and labor factions within our party, who see this as a “sign of weakness” towards the bourgeois and a betrayal of the working class.

 - @give_up_labor_ministry: Give up the Labor Ministry to the Centre Party
 - @give_up_interior_ministry: Give up the Interior Ministry to the Centre Party
 - @give_up_finance_ministry: Give up the Finance Ministry to the DVP
 - @give_up_economic_ministry: Give up the Economic Ministry to the DVP
 - @give_up_justice_ministry: Give up the Justice Ministry to the DDP
 - @give_up_foreign_ministry: Give up the Foreign Ministry to the DVP
 - @give_up_agriculture_ministry: Give up the Agriculture Ministry to the DDP
 - @give_up_reichwehr_ministry: Give up the Reichswehr Ministry to General Groener 

 - @give_up_labor_ministry
choose-if: labor_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Labor Ministry
subtitle: This will upset labor and the unions.
on-arrival: coalition_dissent -= 1; labor_minister_party = “Z”; labor_minister = “Stegerwald”; labor_dissent += 15; left_dissent += 15; z_relation += 5

Fine, we have given up the Labor Ministry. However, the bourgeois see this ministry as a “less important” ministry and are still deeply suspicious of our actions. The Labor and Left factions also sees this as a betrayal and an give to the bourgeois.

 - @give_up_interior_ministry
choose-if: interior_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Interior Ministry
subtitle: This all upset all factions of our party
on-arrival : coalition_dissent -= 1; interior_minister_party = “Z”; interior_minister = “Wirth”; reformist-dissent += 5; left_dissent += 15; centrist_dissent += 10; labor_dissent += 5; z_relation += 5

Fine, we have given up the Interior Ministry. This decision is opposed by all factions of our party. Leftists see this as a complete sell-out to the bourgeois. Even reformists are concerned with our decision, and the bourgeois is still suspicious of us, despite giving up the Interior Ministry. 

 - @give_up_finance_ministry
choose-if: finance_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Finance Ministry
subtitle: This will displease the Labor and Left factions.
on-arrival: finance_minister_party = “DVP”; finance_minister = “Moldenhauer”; coalition_dissent -= 2; centrist_dissent += 10; leftist_dissent += 15; labor_dissent += 10; dvp_relation += 5;

Fine, we have given up the Finance Ministry. This is criticized by centrists, labor, and leftists as another handover to the bourgeois. The DVP is pleased to see us give up the finance ministry, seeing an end to our “socialist” financial policies. 

 - @give_up_economic_ministry
choose-if: economic_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Economic Ministry
subtitle: The will [? if year >= 1929 and month >= 10: significantly ?] upset the Labor and Left factions.
on-arrival: economic_minister_party = “DVP”; economic_minister = “Curtius”; coalition_dissent -= 2; labor_dissent += 10; labor_dissent += 5 if year >= 1929 and month >= 10; centrist_dissent += 5; reformist_dissent += 5; leftist_dissent += 15; leftist_dissent += 5 if year >= 1929 and month >= 10; ddp_relation += 5;

Fine, we have given up the Economic Ministry. This action is opposed by all factions of our party. [? if year >= 1929 and month >= 10: The Labor and Left factions are disgraced by this action, preventing them from implementing their economic plans in the midst of this crisis ?]

 - @give_up_justice_ministry
choose-if: justice_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Justice Ministry
subtitle: This is opposed by all factions of our party [? if judicial_reform <= 2: , including Neorevisionists ?]
on-arrival: justice_minister_party = “DDP”; justice_minister = “Koch-Weser”; coalition_dissent -= 2; labor_dissent += 5; centrist_dissent += 5; reformist_dissent += 5; reformist_dissent += 5 if judicial_reform <= 2; leftist_dissent += 15; neorevisionist_dissent += 5 if judicial_reform <= 2;

Fine, we have given up the Justice Ministry. Reformists are [? if judicial_reform > 2: bothered ?][ if judicial_reform <= 2: disgusted ?] by the action, seeing it as compromising the stability of the Republic, while leftists strongly oppose the action due to another handover to the bourgeois. [? if judicial_reform <= 2:  Neorevisionists also oppose our action due to its perceived harm to the Republic. ?]

 - @give_up_foreign_ministry
choose-if: foreign_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Foreign Ministry
subtitle: This is opposed by the Centrist faction, especially Breitscheid, and the Left faction
on-arrival: foreign_minister_party = “DVP”; foreign_minister = “Stresemann” if stresemann_dead = 0; foreign_minister = “Curtis” if stresemann_dead = 1; coalition_dissent -= 2; centrist_dissent += 10; leftist_dissent += 15; dvp_relation += 10; z_relation += 5 if z_party_leader = “Kaas”;

Fine, we have given up the Foreign Ministry. The DVP is pleased by this decision, while all factions of our party are united against this decision. Leftists oppose this action more strongly than any faction, wanting no overtures to the bourgeois. 

 - @give_up_agriculture_ministry
choose-if: agriculture_minister_party = “SPD”;
unavailable-subtitle: We do not hold the Agriculture Ministry
subtitle: This action is opposed the the Left and Labor factions
on-arrival: agriculture_minister_party = “DDP”; agriculture_minister = “Dietrich”; labor_dissent += 5; leftist_dissent += 10; leftist_dissent += 5 if land_reform < 1; ddp_relation += 5; coalition_dissent -= 2;

Fine, we have given up the Agriculture Ministry. Labor and leftists in our party oppose this decision due to their strong interest in agricultural policy, [? if land_reform < 1: especially land reform. ?]

 - @give_up_reichswehr_ministry
choose-if: reichswehr_ministry_party = “SPD”;
unavailable-subtitle: We do not hold the Reichswehr Ministry
subtitle: This action is opposed by all factions of our party, especially leftists and neorevisionists
on-arrival: reichswehr_minister_party = “I”; reichswehr_minister = “Groener”; leftist_dissent += 15; neorevisionist_dissent += 10; reformist_dissent += 5; labor_dissent += 5; centrist_dissent += 5; dvp_relation += 10; z_relation += 10 if z_party_leader = “Kaas”; z_relation += 5 if z_party_leader = “Joos”; coalition_dissent = 0;

Fine, we have given up the Reichswehr Ministry. This decision scares neorevisionists and leftists who do not want to see the military in the hands of reactionary, or worse, fascist forces. Other factions are slightly opposed to this policy. The Centre and DVP are pleased that we gave up the Reichswehr Ministry. 


@resources
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@support_kpd
choose-if: (in_grand_coalition and communist_coalition >= 3 and kpd_relation >= 50 and kpd_r >= dvp_r) or (in_weimar_coalition and kpd_relation >= 50 and kpd_r >= z_r and communist_coalition >= 3)
unavailable-subtitle: [? if communist_coalition < 3 : The KPD won't take part in parliamentary government. ?] [? if kpd_relation < 50 : The KPD is not friendly enough with us. ?] [? if (in_grand_coalition and kpd_r < dvp_r) or (in_weimar_coalition and kpd_r < z_r): The KPD does not have enough votes. ?] [? if in_popular_front : We are already in a Popular Front with the KPD. ?]

If the Communists abstain, then we essentially can maintain a minority government[? if president=="Hindenburg":, although it would be very difficult to make policy without the support of President Hindenburg, which we do not have ?]. If this happens, then we will own a massive debt to the KPD. This could lead to broader cooperation in the future, for better or for worse.

- @support_kpd_2: Persuade the KPD to "tolerate" us?
- @let_it_happen: Never mind. Let the vote happen.

@support_kpd_2
on-arrival: communist_coalition += 1; workers_kpd += 3; kpd_relation += 5
go-to: support_kpd_weimar if in_weimar_coalition; support_kpd_grand if in_grand_coalition

The KPD is not interested in a "toleration" arrangement; they will only support us if they can participate in the government as part of a coalition. And, if this toleration agreement happens, the right-wing parties will not be inclined to support us. 

@support_kpd_grand

- @election_1928.popular_front_coalition: Form a "Popular Front" coalition?
- @election_1928.left_coalition
- @try_assuage: Never mind. We must deal with this some other way.

@support_kpd_weimar

- @election_1928.left_coalition
- @try_assuage: Never mind. We must deal with this some other way.


@let_it_happen
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "DDP"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "DVP"; finance_minister_party = "DVP"; agriculture_minister_party = "DDP"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann" if stresemann_dead = 0; foreign_minister = "Curtis" if stresemann_dead = 1; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener";  coalition_dissent = 0; pro_republic -= 5; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50
go-to: let_it_happen_hindenburg if president != "Braun"; let_it_happen_braun if president == "Braun"

@let_it_happen_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; welfare -= 2;

The vote succeeds, and our government has resigned. Chancellor Brüning has been appointed as the head of an acting government, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_braun
on-arrival: spd_caretaker = 1

The vote succeeds, and our government has resigned. Chancellor [+ chancellor +] remains as the head of a caretaker government, but no longer has the power to enact new policies.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
