title: The Death of Horst Wessel
view-if: year == 1930 and month >= 2
frequency: 1000
max-visits: 1
tags: event
on-arrival: nsdap_effectiveness += 0.1; workers_nsdap += 3*(nsdap_effectiveness); new_middle_nsdap += 3*(nsdap_effectiveness); old_middle_nsdap += 3*(nsdap_effectiveness); rural_nsdap += 5*(nsdap_effectiveness); unemployed_nsdap += 5*(nsdap_effectiveness); sa_strength += 100*(nsdap_effectiveness); sa_militancy += 0.02*(nsdap_effectiveness); pro_republic -= 3; 

Horst Wessel, a member of the Nazi SA has been killed by, supposedly, two Communists. The Nazi Party sees him as a martyr, and has used this opportunity to increase recuriting to the SA and gain support for their party. How should we respond to this events.

- @arm_rb_2: Increase funding to the Reichsbanner to keep up with the growing strength of the SA. -2 resources
- @warn: This is a concerning development and our members need to stay vigilant about the Nazis. 
- @do_nothing: This is just a one-off event for the Nazis, it is no cause for concern.

@arm_rb_2
choose-if: resources >= 2
on-arrival: resources -= 2; rb_militancy += 0.02; rb_strength += 100; nazi_urgency += 1; pro_republic += 2; nsdap_effectiveness -= 0.03; reformist_dissent -= 5; neorevisionist_dissent -= 15 if neorevisionism == 1; left_dissent -= 10;

If the Nazis are going to toughen up, we need to return the same gesture in order to stay ahead of the Nazis.

@warn
on-arrival: pro_republic += 1; nazi_urgency += 1; center_dissent -= 5; left_dissent -= 5; reformist_dissent -= 5; labor_dissent -= 5; neorevisionist_dissent -= 10 if neorevisionism == 1; 

We, as a party need to stay cautious and united about the Nazi threat. Hearing the call for unity during this crisis, dissent has been reduced among all fasctions [? if neorevisionism == 1: especially Neorevisionists ?]. SPD members have taken the Nazi threat more seriously.

@do_nothing
on-arrival: nsdap_effectiveness += 0.03; pacifism += 1; rb_militancy -= 0.02; rb_strength -= 50; left_dissent += 5; neorevisionist_dissent += 10 if neorevisionism == 1

By treating this event as a one-time event, the Nazis stand to gain, knowing that they are not being taken seriously. Some of our more committed members of the Reichsbanner has left, noting our inaction on the fascist threat. 
